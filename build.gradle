group = 'com.strongloop';

buildscript {
  repositories {
      mavenCentral()
  }
  dependencies {
      classpath 'com.android.tools.build:gradle:0.6.+'
  }
}

apply plugin: 'maven'
apply plugin: 'android-library'

def mavenLocalUrl

repositories {
    def local = mavenLocal();
    mavenCentral()

    // Save MavenLocal url, we will use it later
    // to configure uploadArchives
    mavenLocalUrl = local.url
}

android {
    compileSdkVersion 18
    buildToolsVersion "18.1.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 14
    }
}

dependencies {
    compile 'com.loopj.android:android-async-http:1.4.3'
    instrumentTestCompile 'com.google.guava:guava:15.0'
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (name.equals(com.android.builder.BuilderConstants.DEBUG)) {
        return; // Skip debug builds.
    }

    // define androidReleaseJar task
    task("android${name.capitalize()}Jar", type: Jar) {
        dependsOn variant.javaCompile
        from variant.javaCompile.destinationDir
    }

    // define androidReleaseJavadoc task
    task("generate${name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.plugin.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
    }
}

afterEvaluate { project ->
    task androidJavadocsJar(type: Jar, dependsOn: generateReleaseJavadoc) {
        classifier = 'javadoc'
        from generateReleaseJavadoc.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.allSource
    }

    artifacts {
        archives androidReleaseJar
        archives androidSourcesJar
        archives androidJavadocsJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                if (System.getenv("BUILD_NUMBER")) {
                    // running on Jenkins
                } else {
                    // local build - publish to Maven local cache
                    repository(url: mavenLocalUrl)
                }

                pom.project {
                    name 'strong-remoting-android'
                    description 'Android client for strong-remoting'
                    url 'https://github.com/strongloop/strong-remoting-android'

                    licenses {
                        license {
                            name 'The MIT License'
                            url 'http://opensource.org/licenses/mit-license.php'
                            distribution 'repo'
                        }
                    }

                    scm {
                        connection 'scm:git@github.com:strongloop/strong-remoting-android.git'
                        developerConnection 'scm:git@github.com:strongloop/strong-remoting-android.git'
                        url 'https://github.com/strongloop/strong-remoting-android'
                    }

                    developers {
                        developer {
                            id 'bajtos'
                            name 'Miroslav Bajtos'
                            email 'miroslav@strongloop.com'
                            organization = 'StrongLoop, Inc.'
                            organizationUrl 'http://strongloop.com/'
                        }
                    }
                }
            }
        }
    }
}
